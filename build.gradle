buildscript {
	repositories {
		google()
		jcenter()
	}

	ext.kotlin_version = "1.5.10"

	dependencies {
		classpath "com.android.tools.build:gradle:4.1.3"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
		classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
		classpath "com.github.dcendents:android-maven-gradle-plugin:2.1"
		classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.17"
	}
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'

android {
	compileSdkVersion 29
	buildToolsVersion "29.0.2"

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	defaultConfig {
		minSdkVersion 15
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

		javaCompileOptions {
			annotationProcessorOptions {
				arguments = [
						"room.schemaLocation": "$projectDir/db-schemas".toString()
				]
			}
		}

		versionName System.getenv("SDK_VERSION") ?: "3.1.0-DEV"
		buildConfigField 'String', 'VERSION_NAME', "\"${versionName}\""
	}

	sourceSets {
		androidTest.assets.srcDirs += files("$projectDir/db-schemas".toString())
	}

	buildFeatures {
		buildConfig = true
	}
}

repositories {
	google()
	jcenter()
}

dependencies {
	implementation "androidx.annotation:annotation:1.2.0"
	implementation "androidx.room:room-runtime:2.3.0"
	implementation "com.jakewharton.timber:timber:4.7.1"
	implementation "com.squareup.moshi:moshi:1.12.0"
	implementation "com.squareup.okhttp3:okhttp:4.9.1"
	implementation "com.squareup.okhttp3:logging-interceptor:4.9.1"
	implementation "com.squareup.retrofit2:retrofit:2.9.0"
	implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
	implementation "com.jakewharton.threetenabp:threetenabp:1.2.1"
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
	implementation "org.koin:koin-core:2.0.1"
	implementation "org.koin:koin-android:2.0.1"

	kapt "androidx.room:room-compiler:2.3.0"
	kapt "com.squareup.moshi:moshi-kotlin-codegen:1.12.0"

	testImplementation "androidx.room:room-testing:2.3.0"
	testImplementation "io.mockk:mockk:1.11.0"
	testImplementation "junit:junit:4.12"

	androidTestImplementation "androidx.room:room-testing:2.3.0"
	androidTestImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
	androidTestImplementation("com.android.support.test.espresso:espresso-core:2.2.2", {
		exclude group: 'com.android.support', module: 'support-annotations'
	})
}

apply from: 'release.gradle'
apply from: 'dokka.gradle'
